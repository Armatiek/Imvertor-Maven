<?xml version="1.0" encoding="UTF-8"?>
<!-- 
 * Copyright (C) 2016 Dienst voor het kadaster en de openbare registers
 * 
 * This file is part of Imvertor.
 *
 * Imvertor is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Imvertor is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Imvertor.  If not, see <http://www.gnu.org/licenses/>.
-->
<xsl:stylesheet 
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:UML="omg.org/UML1.3"
    
    xmlns:imvert="http://www.imvertor.org/schema/system"
    xmlns:ext="http://www.imvertor.org/xsl/extensions"
    xmlns:imf="http://www.imvertor.org/xsl/functions"
    
    xmlns:ekf="http://EliotKimber/functions"

    exclude-result-prefixes="#all"
    version="2.0">

    <xsl:import href="../common/Imvert-common.xsl"/>
    <xsl:import href="../common/Imvert-common-derivation.xsl"/>
    
    <xsl:variable name="stylesheet-code">SHACL</xsl:variable>
    <xsl:variable name="debugging" select="imf:debug-mode($stylesheet-code)"/>
    
    <xsl:output method="text"/>
    
    <xsl:template match="/">
        <xsl:value-of select="imf:ttl-comment(('Generated by', imf:get-config-string('run','version'), ''))"/>
        <xsl:value-of select="imf:ttl-comment(('Generated at', imf:get-config-string('run','start'), ''))"/>
        <xsl:value-of select="imf:ttl-comment(())"/>
        
        <!-- 
            read the configured info 
        -->
        <xsl:apply-templates select="$configuration-shaclrules-file//vocabulary" mode="preamble"/>
      
        <xsl:value-of select="imf:ttl-comment(())"/>
        <!-- 
            process the imvertor info 
        -->
        <xsl:value-of select="imf:ttl-comment(('##Feature Type','',''))"/>
        <xsl:value-of select="imf:ttl(())"/>
        <xsl:apply-templates select="$document-packages" mode="mode-object"/>
        
    </xsl:template>
   
    <xsl:template match="vocabulary" mode="preamble">
        <xsl:value-of select="imf:ttl(('@prefix', concat(prefix,':'), concat('&lt;', URI ,'&gt;'), '.'))"/>
    </xsl:template>
    
    <xsl:template match="imvert:package" mode="mode-object">
        <xsl:apply-templates select="imvert:class" mode="#current"/>
    </xsl:template>
    
    <!--=============== objects ================== -->
    
    <xsl:template match="imvert:class" mode="mode-object">
        <xsl:variable name="this" select="."/>
        
        <xsl:value-of select="imf:ttl-construct($this)"/>
        <xsl:value-of select="imf:ttl((imf:ttl-get-uri-name($this),'skos:prefLabel',imf:ttl-value($this/imvert:name/@original,'2q'),';'))"/>
        <xsl:value-of select="imf:ttl(('rdf:type','owl:Class'))"/>
        
        <xsl:value-of select="imf:ttl(('kkg:indicatieAbstractObject ',if (imf:boolean($this/imvert:abstract)) then imf:ttl-value($this/imvert:abstract,'2q') else ()))"/>

        <xsl:value-of select="for $super in imf:get-superclass($this) return imf:ttl(('rdfs:subClassOf',imf:ttl-get-uri-name($super)))"/>
        
        <!-- loop door alle attributen en associaties heen, en plaats een property (predicate object), dus een link naar het attribuut -->
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attribute')]" mode="mode-object"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attributegroup')]" mode="mode-object"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-enum')]" mode="mode-object"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-referentie-element')]" mode="mode-object"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-union-element')]" mode="mode-object"/>
        
        <xsl:apply-templates select="$this/imvert:associations/imvert:association[imvert:target/imvert:stereotype = imf:get-config-stereotypes('stereotype-name-relation-role')]" mode="mode-object"/>
        
        <xsl:sequence select="imf:ttl-get-all-tvs($this)"/>
     
        <!-- specific properties of class types -->
        
        <!-- stereotype-name-objecttype -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-objecttype')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:objecttype'))"/>
        </xsl:if>
        <!-- stereotype-name-composite -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-composite')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:composite'))"/>
        </xsl:if>
        <!-- stereotype-name-koppelklasse -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-koppelklasse')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:koppelklasse'))"/>
        </xsl:if>
        <!-- stereotype-name-relatieklasse -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-relatieklasse')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:relatieklasse'))"/>
        </xsl:if>
        <!-- stereotype-name-enumeration -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-enumeration')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:enumeration'))"/>
        </xsl:if>
        <!-- stereotype-name-codelist -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-codelist')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:codelist'))"/>
        </xsl:if>
        <!-- stereotype-name-complextype -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-complextype')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:complextype'))"/>
        </xsl:if>
        <!-- stereotype-name-simpletype -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-simpletype')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:simpletype'))"/>
        </xsl:if>
        <!-- stereotype-name-referentielijst -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-referentielijst')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:referentielijst'))"/>
        </xsl:if>
        <!-- stereotype-name-union -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-union')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:union'))"/>
        </xsl:if>
        
        <xsl:value-of select="imf:ttl('.')"/>
        
        <!-- loop door alle attributen en associaties heen, en maak daarvoor een subject -->
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attribute')]" mode="mode-subject"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attributegroup')]" mode="mode-subject"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-enum')]" mode="mode-subject"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-referentie-element')]" mode="mode-subject"/>
        <xsl:apply-templates select="$this/imvert:attributes/imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-union-element')]" mode="mode-subject"/>
        
        <xsl:apply-templates select="$this/imvert:associations/imvert:association[imvert:target/imvert:stereotype = imf:get-config-stereotypes('stereotype-name-relation-role')]" mode="mode-subject"/>
        
    </xsl:template>
    
    <xsl:template match="imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attribute')]" mode="mode-object">
        <xsl:variable name="this" select="."/>
        <xsl:value-of select="imf:ttl(('kkg:bezitAttribuutsoort',imf:ttl-get-uri-name($this)))"/>
    </xsl:template>
    
    <xsl:template match="imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attributegroup')]" mode="mode-object">
        <xsl:variable name="this" select="."/>
        <xsl:value-of select="imf:ttl(('kkg:Gegevensgroep',imf:ttl-get-uri-name($this)))"/>
    </xsl:template>
    
    <xsl:template match="imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-enum')]" mode="mode-object">
        <xsl:variable name="this" select="."/>
        <xsl:value-of select="imf:ttl(('kkg:enum',imf:ttl-get-uri-name($this)))"/>
    </xsl:template>

    <xsl:template match="imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-referentie-element')]" mode="mode-object">
        <xsl:variable name="this" select="."/>
        <xsl:value-of select="imf:ttl(('kkg:referentieElement',imf:ttl-get-uri-name($this)))"/>
    </xsl:template>
    
    <xsl:template match="imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-union-element')]" mode="mode-object">
        <xsl:variable name="this" select="."/>
        <xsl:value-of select="imf:ttl(('kkg:unionElement',imf:ttl-get-uri-name($this)))"/>
    </xsl:template>
    
    <xsl:template match="imvert:attribute" mode="mode-object">
        <xsl:sequence select="imf:msg(.,'ERROR','Unknown attribute stereo: [1]', imvert:stereotype)"/>
    </xsl:template>
    
    <xsl:template match="imvert:association" mode="mode-object">
        <xsl:variable name="this" select="."/>
        <xsl:value-of select="imf:ttl(('kkg:bezitRelatierol',imf:ttl-get-uri-name($this)))"/>
    </xsl:template>
    
    <!--=============== subjects ================== -->
    
    <xsl:template match="imvert:attribute" mode="mode-subject">
        <xsl:variable name="this" select="."/>
        
        <xsl:value-of select="imf:ttl-construct($this)"/>
        <xsl:value-of select="imf:ttl((imf:ttl-get-uri-name($this),'skos:prefLabel',imf:ttl-value($this/imvert:name/@original,'2q'),';'))"/>
        
        <xsl:variable name="defining-class" select="imf:ttl-get-defining-class($this)"/>
        <xsl:choose>
            <xsl:when test="exists($defining-class)">
                <xsl:value-of select="imf:ttl(('kkg:domeinType',imf:ttl-get-uri-name($defining-class)))"/>
            </xsl:when>
            <xsl:when test="exists($defining-class)">
                <xsl:value-of select="imf:ttl(('kkg:domeinType',imf:ttl-get-uri-name($defining-class)))"/>
            </xsl:when>
            <xsl:otherwise>
                <xsl:variable name="datatype-name" select="imvert:type-name"/>
                <xsl:value-of select="imf:ttl(('kkg:domeinType', concat('primitive:',$datatype-name)))"/>
            </xsl:otherwise>
        </xsl:choose>
        
        <xsl:value-of select="imf:ttl(('kkg:identificerend',imf:ttl-value($this/imvert:is-id,'2q')))"/>
        <xsl:value-of select="imf:ttl(('prov:wasDerivedFrom',imf:ttl-value($this/imvert:is-value-derived,'2q')))"/>
        
        <xsl:value-of select="imf:ttl(('kkg:minKardinaliteit',if ($this/imvert:min-occurs != '1') then imf:ttl-value($this/imvert:min-occurs,'2q') else ()))"/>
        <xsl:value-of select="imf:ttl(('kkg:maxKardinaliteit',if ($this/imvert:max-occurs != '1') then imf:ttl-value($this/imvert:max-occurs,'2q') else ()))"/>
        
        <xsl:sequence select="imf:ttl-get-all-tvs($this)"/>
        
        <!-- stereotype-name-attribute -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attribute')">
            <xsl:value-of select="imf:ttl(('rdf:type','kkg:Attribuutsoort'))"/>     
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:attribute'))"/>
        </xsl:if>
        <!-- stereotype-name-attributegroup -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attributegroup')">
            <xsl:value-of select="imf:ttl(('rdf:type','kkg:Attribuutgroep'))"/>
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:attributegroup'))"/>
        </xsl:if>
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-enum')">
            <xsl:value-of select="imf:ttl(('rdf:type','kkg:Enum'))"/>     
        </xsl:if>
        <!-- stereotype-name-identificatie -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-identificatie')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:identificatie'))"/>
        </xsl:if>
        
        <xsl:value-of select="imf:ttl('.')"/>        
        
    </xsl:template>
    
    <xsl:template match="imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-enum')]" mode="mode-subject">
        <xsl:variable name="this" select="."/>
        
        <xsl:value-of select="imf:ttl-construct($this)"/>
        <xsl:value-of select="imf:ttl((imf:ttl-get-uri-name($this),'skos:prefLabel',imf:ttl-value($this/imvert:name/@original,'2q'),';'))"/>
        
        <xsl:sequence select="imf:ttl-get-all-tvs($this)"/>
        
        <xsl:value-of select="imf:ttl(('rdf:type','kkg:Enum'))"/>     
        
        <xsl:value-of select="imf:ttl('.')"/>        
    </xsl:template>
    
    <xsl:template match="imvert:attribute[imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attribute')]" mode="mode-datatype">
        <xsl:variable name="this" select="."/>
        <xsl:choose>
            <xsl:when test="empty(imvert:type-id)">
            
                <xsl:variable name="datatype-name" select="imf:ttl-get-uri-name($this)"/>
                <xsl:value-of select="imf:ttl(($datatype-name,'rdf:type', 'kkg:PrimitiefDatatype',';'))"/>
                <xsl:value-of select="imf:ttl-comment(('more?'))"/>
                <xsl:value-of select="imf:ttl('.')"/>
                
            </xsl:when>
        </xsl:choose>
        
    </xsl:template>
        
    <xsl:template match="imvert:association" mode="mode-subject">
        <xsl:variable name="this" select="."/>
        
        <xsl:value-of select="imf:ttl-construct($this)"/>
        <xsl:value-of select="imf:ttl((imf:ttl-get-uri-name($this),'skos:prefLabel',imf:ttl-value($this/imvert:target/imvert:role/@original,'2q'),';'))"/>
        
        <xsl:variable name="defining-class" select="imf:ttl-get-defining-class($this)"/>
        <xsl:value-of select="imf:ttl(('kkg:gerelateerdObjecttype',imf:ttl-get-uri-name($defining-class)))"/>

        <xsl:value-of select="imf:ttl(('kkg:identificerend',imf:ttl-value($this/imvert:is-id,'2q')))"/>
        <xsl:value-of select="imf:ttl(('prov:wasDerivedFrom',imf:ttl-value($this/imvert:is-value-derived,'2q')))"/>
        <xsl:value-of select="imf:ttl(('kkg:typeAggregatie',if (imvert:aggregation = 'composite') then imf:ttl-value('composite','2q') else ()))"/>
        
        <xsl:value-of select="imf:ttl(('kkg:minKardinaliteit',imf:ttl-value($this/imvert:min-occurs,'2q')))"/>
        <xsl:value-of select="imf:ttl(('kkg:maxKardinaliteit',imf:ttl-value($this/imvert:max-occurs,'2q')))"/>
        
        <xsl:sequence select="imf:ttl-get-all-tvs($this/imvert:target)"/>
        
        <!-- stereotype-name-attribute -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attribute')">
            <xsl:value-of select="imf:ttl(('rdf:type','kkg:Attribuutsoort'))"/>     
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:attribute'))"/>
        </xsl:if>
        <!-- stereotype-name-attributegroup -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-attributegroup')">
            <xsl:value-of select="imf:ttl(('rdf:type','kkg:Attribuutgroep'))"/>
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:attributegroup'))"/>
        </xsl:if>
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-enum')">
            <xsl:value-of select="imf:ttl(('rdf:type','kkg:Enum'))"/>     
        </xsl:if>
        <!-- stereotype-name-identificatie -->
        <xsl:if test="imvert:stereotype = imf:get-config-stereotypes('stereotype-name-identificatie')">
            <xsl:value-of select="imf:ttl(('uml:stereotype','kkg:identificatie'))"/>
        </xsl:if>
        
        <xsl:value-of select="imf:ttl('.')"/>        
        
    </xsl:template>
    
    <xsl:template match="node()" mode="#all">
        <!-- skip -->        
    </xsl:template>
    
    <!-- formatteren van de TTL: 
        als drie of meer items, spatie gescheiden aan elkaar plakken.
        als twee, dan eindigen met ';';
        anders overslaan.
    -->
    <xsl:function name="imf:ttl" as="xs:string?">
        <xsl:param name="parts" as="item()*"/>
        <xsl:choose>
            <xsl:when test="$parts[3]">
                <xsl:value-of select="concat(string-join($parts,' '),'&#10;')"/>
            </xsl:when>
            <xsl:when test="$parts[2]">
                <xsl:value-of select="concat('   ', $parts[1],' ',$parts[2],' ;','&#10;')"/>
            </xsl:when>
            <xsl:when test="$parts[1] = '.'">
                <xsl:value-of select="concat('.','&#10;&#10;')"/>
            </xsl:when>
            <xsl:otherwise>
                <!-- skip -->
            </xsl:otherwise>
        </xsl:choose>
    </xsl:function>
    
    <xsl:function name="imf:ttl-construct" as="xs:string">
        <xsl:param name="this" as="item()*"/>
        <xsl:value-of select="imf:ttl-comment(('Construct:',imf:get-display-name($this), concat('(', string-join($this/imvert:stereotype,', ') ,')')))"/>
    </xsl:function>
    
    <xsl:function name="imf:ttl-comment" as="xs:string">
        <xsl:param name="parts" as="item()*"/>
        <xsl:value-of select="concat(if ($parts[1]) then '#' else '', string-join($parts,' '),'&#10;')"/>
    </xsl:function>
    
    <xsl:variable name="str3quot">'''</xsl:variable>
    <xsl:variable name="str2quot">"</xsl:variable>
    <xsl:variable name="str1quot">'</xsl:variable>
    
    <!-- return (name, type) sequence -->
    <xsl:function name="imf:ttl-map" as="element(map)?">
        <xsl:param name="id"/>
        <xsl:sequence select="$configuration-shaclrules-file//node-mapping/map[@id=$id]"/>
    </xsl:function>
    
    <xsl:function name="imf:ttl-value" as="xs:string?">
        <xsl:param name="string" as="xs:string?"/>
        <xsl:param name="type" as="xs:string?"/>
        <xsl:choose>
            <xsl:when test="not(normalize-space($string))">
                <!-- skip -->
            </xsl:when>
            <xsl:when test="$type = '3q'">
                <xsl:value-of select="concat($str3quot,$string,$str3quot)"/>
            </xsl:when>
            <xsl:when test="$type = '2q'">
                <xsl:value-of select="concat($str2quot,$string,$str2quot)"/>
            </xsl:when>
            <xsl:otherwise>
                <xsl:value-of select="concat($str1quot,$string,$str1quot)"/>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:function>
    
    <!-- 
        Haal alle tagged values op in TTL statement formaat.
        Dit zijn alle relevante tv's, dus ook die waarvan de waarde is afgeleid.
    -->
    <xsl:function name="imf:ttl-get-all-tvs">
        <xsl:param name="this"/>
        <!-- loop door alle tagged values heen -->
        <xsl:for-each select="imf:get-config-applicable-tagged-value-ids($this)">
            <xsl:variable name="tv" select="imf:get-most-relevant-compiled-taggedvalue-element($this,concat('##',.))"/>
            <xsl:variable name="map" select="imf:ttl-map($tv/@id)"/>
            <xsl:if test="exists($tv) and exists($map)">
                <xsl:value-of select="imf:ttl(($map, imf:ttl-value($tv/@value,$map/@type)))"/>
            </xsl:if>
        </xsl:for-each>
    </xsl:function>
    
    <!-- 
        return for passed attribute or assoc the class when this is defined in terms of classes 
    -->
    <xsl:function name="imf:ttl-get-defining-class" as="element()?">
        <xsl:param name="this"/>
        <xsl:variable name="type-id" select="$this/imvert:type-id"/>
        <xsl:if test="exists($type-id)">
            <xsl:sequence select="$document-classes[imvert:id = $type-id]"/>
        </xsl:if>
    </xsl:function>
    
    <xsl:function name="imf:ttl-get-uri-name">
        <xsl:param name="class"/>
        <!--
        <xsl:variable name="dn" select="subsequence(tokenize(imf:get-display-name($class),'\s\('),1,1)"/>
        <xsl:value-of select="concat('data:', string-join(tokenize($dn,'[^A-Za-z0-9]+'),'_'))"/>
        -->
        <xsl:value-of select="concat('model:', $class/@formal-name)"/>
    </xsl:function>
        
</xsl:stylesheet>
